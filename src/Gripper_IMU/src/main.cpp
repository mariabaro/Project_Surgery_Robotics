#include <WiFi.h>
#include <WiFiUdp.h>
#include "MPU9250.h"
#include <Wire.h>
#include <ArduinoJson.h>  // No cal .hpp, la versió 7 manté compatibilitat amb .h
#include <IMU_RoboticsUB.h>   // Nom de la llibreria custom

// Device ID
const char *deviceId = "G3_Gri";

// Wi-Fi credentials
const char *ssid = "Robotics_UB";
const char *password = "rUBot_xx";

// Vibration motor settings
const int vibrationPin = 23; // Pin for the vibration motor

// Botons
const int PIN_S1 = 14;
const int PIN_S2 = 27;
int s1Status = HIGH;
int s2Status = HIGH;

// UDP settings
IPAddress receiverESP32IP(192, 168, 1, 33); // IP of receiver ESP32
IPAddress receiverComputerIP(192, 168, 1, 55); // IP of PC
const int udpPort = 12345;
WiFiUDP udp;

// IMU object
IMU imu;

// Orientation data
float Gri_roll = 0.0, Gri_pitch = 0.0, Gri_yaw = 0.0;

// Torque data received from G3_Servos
float Torque_roll1 = 0.0, Torque_roll2 = 0.0, Torque_pitch = 0.0, Torque_yaw = 0.0;

void connectToWiFi() {
  Serial.print("Connecting to Wi-Fi");
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }
  Serial.println("\nWi-Fi connected!");
  Serial.println("IP Address: " + WiFi.localIP().toString());
  Serial.print("ESP32 MAC Address: ");
  Serial.println(WiFi.macAddress());
}

// Modified function to update orientation from IMU
void updateOrientation() {
  imu.ReadSensor();
  float* rpw = imu.GetRPW();
  Gri_roll  = rpw[0];
  Gri_pitch = rpw[1];
  Gri_yaw   = rpw[2];
  s1Status = digitalRead(PIN_S1);
  s2Status = digitalRead(PIN_S2);
}

void sendOrientationUDP() {
  JsonDocument doc;
  doc["device"] = deviceId;
  doc["roll"] = Gri_roll;
  doc["pitch"] = Gri_pitch;
  doc["yaw"] = Gri_yaw;
  doc["s1"] = s1Status;
  doc["s2"] = s2Status;

  char jsonBuffer[512];
  serializeJson(doc, jsonBuffer);

  // Send to ESP32 Servos
  udp.beginPacket(receiverESP32IP, udpPort);
  udp.write((const uint8_t*)jsonBuffer, strlen(jsonBuffer));
  udp.endPacket();

  // Send to Computer
  udp.beginPacket(receiverComputerIP, udpPort);
  udp.write((const uint8_t*)jsonBuffer, strlen(jsonBuffer));
  udp.endPacket();
}

// Added function to receive torques from UDP
void receiveTorquesUDP() {
  int packetSize = udp.parsePacket();
  if (packetSize) {
    byte packetBuffer[512];
    int len = udp.read(packetBuffer, 512);
    if (len > 0) {
      packetBuffer[len] = '\0';

      JsonDocument doc;
      DeserializationError error = deserializeJson(doc, packetBuffer);
      if (error) {
        Serial.print(F("deserializeJson() failed: "));
        Serial.println(error.f_str());
        return;
      }

      const char* device = doc["device"];
      if (strcmp(device, "G3_Servos") == 0) {
        Torque_roll1 = doc["Torque_roll1"];
        Torque_roll2 = doc["Torque_roll2"];
        Torque_pitch = doc["Torque_pitch"];
        Torque_yaw   = doc["Torque_yaw"];

        Serial.print("Received torques → ");
        Serial.print("Roll1: "); Serial.print(Torque_roll1, 3);
        Serial.print(" | Roll2: "); Serial.print(Torque_roll2, 3);
        Serial.print(" | Pitch: "); Serial.print(Torque_pitch, 3);
        Serial.print(" | Yaw: "); Serial.println(Torque_yaw, 3);

        float totalTorque = Torque_roll1 + Torque_pitch + Torque_yaw;
        int vibrationValue = constrain(totalTorque * 2.5, 0, 255); // Scale factor
        ledcWrite(0, vibrationValue); // Set vibration motor intensity
        Serial.print("Vibration motor value: ");
        Serial.println(vibrationValue); 
      }
    }
  }
}

void setup() {
  Serial.begin(115200);
  Wire.begin();
  delay(2000);

  // Initialize IMU
  imu.Install();

  connectToWiFi();
  udp.begin(udpPort);
  Serial.println("UDP initialized.");

  // Setup pins for buttons
  ledcSetup(0, 5000, 8); // Setup channel 0 at 5kHz, 8-bit resolution
  ledcAttachPin(vibrationPin, 0); // Attach vibration motor to channel 0
}

// Modified Loop function
void loop() {
  updateOrientation();    
  sendOrientationUDP();   
  receiveTorquesUDP();    // Receive torque data from servos via UDP
  delay(10);
}